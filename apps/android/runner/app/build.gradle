apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    if (gradle.ext.useLocalLibrary) {
        flatDir {
            dirs gradle.ext.localLibraryDir
        }
    }

    if (gradle.ext.useRemoteLibrary) {
        flatDir {
            dirs gradle.ext.remoteLibraryDir
        }
    }

    google()
}

android {
    compileSdkVersion 33
    flavorDimensions "default"
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId 'com.nativium.app'
        minSdkVersion 16
        targetSdkVersion 33
        versionCode 1
        versionName '1.0.0'
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias '[YOUR-KEY-ALIAS]'
            keyPassword '[YOUR-KEY-PASSWORD]'
            storeFile file('keystore.release.jks')
            storePassword '[YOUR-STORE-PASSWORD]'
        }
    }

    android {
        lintOptions {
            checkReleaseBuilds false
            abortOnError false
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            versionNameSuffix '-DEBUG'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    if (gradle.ext.useLocalLibrary || gradle.ext.useRemoteLibrary) {
        if (gradle.ext.isDebuggable) {
            implementation(name: "library-debug", ext: "aar")
        } else {
            implementation(name: "library-release", ext: "aar")
        }
    } else {
        implementation project(':nativium-library')
    }

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.0'

    implementation 'com.google.android.material:material:1.6.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'

    implementation 'com.google.android.instantapps:instantapps:1.1.0'

    // test only
    implementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

// custom download task
if (gradle.ext.useRemoteLibrary) {
    apply plugin: "de.undercouch.download"

    task downloadNativiumLibraryFile(type: Download) {
        src "https://nativium.s3.amazonaws.com/dist/android/${gradle.ext.nativiumLibraryVersion}/dist.tar.gz"
        dest new File(gradle.ext.nativiumLibraryCacheDir, "nativium-library/library-${gradle.ext.nativiumLibraryVersion}.tar.gz")
        tempAndMove true
        onlyIfModified true
        overwrite false
        downloadTaskDir gradle.ext.nativiumLibraryCacheDir
    }

    task getNativiumLibrary(dependsOn: downloadNativiumLibraryFile, type: Copy) {
        from tarTree(downloadNativiumLibraryFile.dest)
        into new File(buildDir, "nativium-library")
    }

    preBuild.dependsOn getNativiumLibrary
}
